extensions
[
  csv
]

globals
[
  cell-lifespan               ;; The average life span in tick of cell
  cell-quiescent-count        ;; The total number of cell enter quiscent state occurences
  cell-somatic-spawn-count     ;; The total number of somatic cells that spawn during the simulation
  cell-malignant-spawn-count  ;; The total number of malignant cells that spawn during the simulation
]

;; Define the agentsets and agents for cells.
breed [cells cell]


;; Agent variables that define the cancer and normal cells of the model.
cells-own
[
  malignant?            ;; TRUE, if cell is a cancer cell
  age                   ;; INTEGER, age of cells in hours
  energy                ;; FLOAT, amount of energy the cell has
  low-energy-duration   ;; INTEGER, time in hours the cell has spent at risk of undergoing apoptosis
  proliferation-chance  ;; FLOAT, probability of proliferating at each tick
  quiescent             ;; INTEGER, the duration in tick for the cell to be in quiescent state
  local-artery-density  ;; INTEGER, density of artery patches in a radius of 2 from the nascent cancer cell
]


;; Patches in this model are either Extracellular Matrix (ECM) or artery cells
patches-own
[
  vascular?        ;; TRUE if endemic artery or TRUE if angiogensis occurs on patch
  nutrient         ;; FLOAT, the combined nutrient level on a patch from initialisation and diffusion
                   ;; nutrient represents Oxygen, sugar and other ions that are essential for cells
]


;; Generates the initial vasculature that surrounds the tumour/normal cell milleu. To be viable
;; vasculature, arteries must be contiguous. The vasulature generated by generate-vasculate represents
;; the arteriolar structure that exists at the tumour/normal tissue boundary.
to generate-vasculature
  ask patches [set vascular? false]
  foreach (list min-pxcor 0 max-pxcor) [ x ->
    foreach (list min-pycor 0 max-pycor) [ y ->
      foreach (list min-pzcor 0 max-pzcor) [ z ->
        ask patches with [pycor = y and pzcor = z]
        [
          become-vascular
        ]
        ask patches with [pxcor = x and pzcor = z]
        [
          become-vascular
        ]
        ask patches with [pxcor = x and pycor = y]
        [
          become-vascular
        ]
      ]
    ]
  ]

  ;; Extracellular matrix [ECM] (black patches) begin with a small amount of nutrient to represent baseline levels
  ask patches with [not vascular?] [set nutrient random 3]
  repeat 100 [
    repeat 10 [ replenish-nutrient ]
    diffuse nutrient 0.1
  ]
end


;; Initialises patch variables to represent an artery patch.
;; Artery nutrient level set to 100 to provide sufficient nutrient
;; to the ECM and cells at initialisation. This is to realistically
;; represent ample nutrient cycling through the vascular system.
to become-vascular
  set nutrient 100
  set vascular? true
end


;; Generates the cell numbers and ratio of cancer to normal cells at initialisation.
to generate-cells
  set-default-shape cells "circle"
  set cell-quiescent-count 0
  set cell-somatic-spawn-count 0
  set cell-malignant-spawn-count 0

  ask n-of initial-cells (patches with [not vascular?])
  [
    sprout-cells 1
    [
      set age (1 + random cell-lifespan)
      set energy (3 + random 6)
      set quiescent 0
      ;; cells at initialisation do not count towards local-artery-density
      set local-artery-density -1

      ifelse (random-float 100 <= disease-ratio)
      [
        set malignant? true
        set color green
        set proliferation-chance malignant-proliferation-chance
      ]
      [
        set malignant? false
        set color blue
        set proliferation-chance normal-proliferation-chance
      ]
    ]
  ]
end


;; Increases the ages of all viable cells by one hour for each tick.
to cell-age
  set age age + 1
end

;; Decrease the quiescent duration of cell after each tick.
to cell-quiescent
  if quiescent > 0
  [
    set quiescent (quiescent - 1)
  ]
end


;; Kills cells with a 50% chance once they have been alive for 50 days and not in a quiescent state.
;; Cells can also undergo apoptosis if their energy is too low for too long and not in quiescent state.
;; Malignant cells have a chance to enter a quiescent state and escape death,
;; they can live for a duration without nutrient intake (low energy).
to cell-die
  if quiescent = 0 and (
    (age > cell-lifespan and random 100 < 50)
    or (low-energy-duration > max-low-energy-duration)
  )
  [
    ifelse (malignant? and random 100 < 20)
    [
      set quiescent quiescent-duration
      set cell-quiescent-count (cell-quiescent-count + 1)
    ]
    [
      die
    ]

  ]
end


;; Counts how many hours (ticks) the cell has been with low energy.
;; Increases by 1 every continuous tick with low energy.
;; Reset to 0 once a cell's energy is above a threshold.
to cell-energy-duration
  ifelse(energy < 1)
  [set low-energy-duration low-energy-duration + 1]
  [set low-energy-duration 0]
end


;; Cells absorb nutrients from their local environment (patch-here).
to cell-absorb-energy
  if [nutrient] of patch-here > 1 [
    ask patch-here [set nutrient (nutrient - 1)]
    set energy (energy + 1)
  ]
end


;; Proliferates new cells, if the parent cell has enough energy.
;; Cells cannot proliferate onto an artery patch.
;; Cells cannot proliferate onto a space that is occupied.
to cell-proliferate
  let patch-neighbor one-of neighbors with [not vascular? and not any? cells-here]

  if patch-neighbor != nobody
  [
    if random 100 < proliferation-chance and energy >= energy-req-prolif and quiescent = 0
    [
      set energy (energy / 2)
      hatch 1
      [
        set age 0
        set quiescent 0
        move-to patch-neighbor
        set local-artery-density count (patches with [vascular?] in-radius 2)
        ifelse malignant? = false
        [ set cell-somatic-spawn-count (cell-somatic-spawn-count + 1)]
        [ set cell-malignant-spawn-count (cell-malignant-spawn-count + 1)]
      ]
    ]
  ]
end


;; Visual procedure. Changes the hue of the red vasculature and opacity of the ECM
;; More red hue == more nutrients in artery/arteriole patch
;; More opaque == more nutrients on patch
to show-nutrient
  ask patches with [nutrient > 1 and not vascular?]
  [
    set pcolor (list 255 0 0 (min (list (nutrient) 255)))
  ]
  ask patches with [vascular?]
  [
    set pcolor (list 255 0 0 (min (list (nutrient * 5) 255)))
  ]
end


;; Increases the nutrient level in the vasculature.Nutrients are restored
;; in the vasculature by the cycling blood of the vascular system.
to replenish-nutrient
  ask patches with [vascular?] [
    set nutrient (nutrient + 0.1)
  ]
end

;; Generates vacscular growth in the extracellular matrix if there are enough cancer cells near to
;; existing vasculature. This represents that concentration of growth factor that an artery/arteriole
;; would receive from nearby cancer cells.
;; Only generates vasculature in a contiguous manner, to represent vascular truncation. Neighbours6 is
;; used instead of neighbours to restrict the truncation direction, making more realistic structures.
to angiogenesis
  ask patches with [not vascular? and count neighbors6 with [vascular?] > 0 and count neighbors with [vascular?] < 4] [
    let demand (count cells with [malignant?] in-radius angiogenesis-signal-radius)
    if demand > angiogenesis-min-demand [
      become-vascular
      set nutrient mean [nutrient] of neighbors with [vascular?]
    ]
  ]
end


;; Set-up procedure that instanties the environment and cell agents.
to setup
  clear-all
  reset-ticks
  set cell-lifespan 50
  generate-vasculature
  generate-cells
  show-nutrient
end


;; Go procedure that runs the simulation.
to go

  ;; Stopping conditions: complete cell death or 300 days of growth.
  if count cells = 0 or ticks > 300 [stop]

  ;; nutrients diffusing through the ECM at a constant rate
  diffuse nutrient 0.01

  show-nutrient
  angiogenesis

   ask cells
  [
    cell-quiescent
    cell-die
    cell-age
    cell-absorb-energy
    cell-proliferate
    cell-energy-duration
  ]
  replenish-nutrient
  tick
end

;; data reporting procedures for statistical analysis
to-report count-malignant-cells
  report count cells with [ malignant? ]
end

to-report count-malignant-spawn-cells
  report cell-malignant-spawn-count
end

to-report count-somatic-cells
  report count cells with [ not malignant? ]
end

to-report count-somatic-spawn-cells
  report cell-somatic-spawn-count
end

to-report count-quiescent-cells-occurences
  report cell-quiescent-count
end

to-report count-quiescent-cells-at-final-tick
  report count cells with [ quiescent > 0 ]
end

to-report count-vascular-patches
  report count patches with [ vascular? ]
end


;; Copyright 2020 Nhan Kiet To (1043668), Adam Thalhammer (972267), Will Walters (583639).
;; See Info table for full copyright and license.
@#$#@#$#@
GRAPHICS-WINDOW
0
0
411
412
-1
-1
13.0
1
10
1
1
1
0
1
1
1
-15
15
-15
15
-15
15
1
1
1
ticks
30.0

BUTTON
17
10
84
43
NIL
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

SLIDER
90
10
270
43
initial-cells
initial-cells
2
1000
500.0
1
1
NIL
HORIZONTAL

SLIDER
90
44
270
77
disease-ratio
disease-ratio
0
100
2.0
1
1
%
HORIZONTAL

BUTTON
18
59
84
92
NIL
go
T
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

MONITOR
18
100
83
145
total cells
count cells
17
1
11

PLOT
17
151
336
301
Cumulative Cancer and Normal Cell Ratios
Days
% Total Pop.
0.0
350.0
0.0
100.0
false
false
"" ""
PENS
"% Cancer Cells" 1.0 0 -13840069 true "" "plot (count cells with [ malignant? ] / count cells) * 100"
"% Normal Cells" 1.0 0 -13345367 true "" "plot (count cells with [ not malignant? ] / count cells) * 100"

PLOT
18
310
336
495
Populations
Days
Count
0.0
350.0
0.0
10.0
true
false
"" ""
PENS
"Cancer Cells" 1.0 0 -10899396 true "" "plot count-malignant-cells"
"Normal Cells" 1.0 0 -13345367 true "" "plot count-somatic-cells"
"Artery " 1.0 0 -2674135 true "" "plot count-vascular-patches"

SLIDER
343
77
543
110
max-low-energy-duration
max-low-energy-duration
0
15
10.0
1
1
ticks
HORIZONTAL

SLIDER
343
110
543
143
energy-req-prolif
energy-req-prolif
0
15
2.0
1
1
NIL
HORIZONTAL

SLIDER
343
176
543
209
normal-proliferation-chance
normal-proliferation-chance
0
5
2.0
0.25
1
%
HORIZONTAL

SLIDER
343
143
543
176
malignant-proliferation-chance
malignant-proliferation-chance
0
5
2.25
0.25
1
%
HORIZONTAL

SLIDER
343
10
543
43
angiogenesis-signal-radius
angiogenesis-signal-radius
0
10
4.0
1
1
NIL
HORIZONTAL

SLIDER
343
43
543
76
angiogenesis-min-demand
angiogenesis-min-demand
0
10
5.0
1
1
NIL
HORIZONTAL

SLIDER
343
209
543
242
quiescent-duration
quiescent-duration
0
20
15.0
1
1
NIL
HORIZONTAL

PLOT
342
251
723
495
Artery Density (Radius 2) of Nascent Cancer Cells
Days
Count
0.0
350.0
0.0
150.0
true
true
"" ""
PENS
"Density 0" 1.0 0 -2674135 true "" "plot count cells with [malignant? and local-artery-density = 0]"
"Density 1" 1.0 0 -2064490 true "" "plot count cells with [malignant? and local-artery-density = 1]"
"Density 2" 1.0 0 -13345367 true "" "plot count cells with [malignant? and local-artery-density = 2]"
"Density 3" 1.0 0 -1184463 true "" "plot count cells with [malignant? and local-artery-density = 3]"
"Density 4" 1.0 0 -7500403 true "" "plot count cells with [malignant? and local-artery-density = 4]"
"Density 5" 1.0 0 -955883 true "" "plot count cells with [malignant? and local-artery-density = 5]"
"Density 6" 1.0 0 -6459832 true "" "plot count cells with [malignant? and local-artery-density = 6]"
"Density 7" 1.0 0 -13840069 true "" "plot count cells with [malignant? and local-artery-density = 7]"
"Density 8" 1.0 0 -5825686 true "" "plot count cells with [malignant? and local-artery-density = 8]"
"Density 9" 1.0 0 -11221820 true "" "plot count cells with [malignant? and local-artery-density = 9]"

@#$#@#$#@
## WHAT IS IT?

This model simulates the growth of a nascent solid tissue tumour, how it interacts with the local tissue (somatic cells and artery vasculature) and how the tumour responds to angiogenesis (the growth of new artery cells).

## HOW IT WORKS

(what rules the agents use to create the overall behavior of the model)

## HOW TO USE IT

(how to use the model, including a description of each of the items in the Interface tab)

## THINGS TO NOTICE

(suggested things for the user to notice while running the model)

## THINGS TO TRY

(suggested things for the user to try to do (move sliders, switches, etc.) with the model)

## EXTENDING THE MODEL

(suggested things to add or change in the Code tab to make the model more complicated, detailed, accurate, etc.)

## NETLOGO FEATURES

(interesting or unusual features of NetLogo that the model uses, particularly in the Code tab; or where workarounds were needed for missing features)

## RELATED MODELS

Tumour from the Netlogo Models library. Any 3D spatial models in the Netlogo Library. Olsen & Siegelmann, (2013) in Procedia Computer Science and Abbott et al., (2006) in Artificial Life.

## CREDITS AND REFERENCES


This model is the original intellectual work of the authors. Copyright 2020 Nhan Kiet To, Adam Thalhammer, Will Walters. It is freely available and distributable for all non-commercial purposes.
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

sheep
false
15
Circle -1 true true 203 65 88
Circle -1 true true 70 65 162
Circle -1 true true 150 105 120
Polygon -7500403 true false 218 120 240 165 255 165 278 120
Circle -7500403 true false 214 72 67
Rectangle -1 true true 164 223 179 298
Polygon -1 true true 45 285 30 285 30 240 15 195 45 210
Circle -1 true true 3 83 150
Rectangle -1 true true 65 221 80 296
Polygon -1 true true 195 285 210 285 210 240 240 210 195 210
Polygon -7500403 true false 276 85 285 105 302 99 294 83
Polygon -7500403 true false 219 85 210 105 193 99 201 83

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

wolf
false
0
Polygon -16777216 true false 253 133 245 131 245 133
Polygon -7500403 true true 2 194 13 197 30 191 38 193 38 205 20 226 20 257 27 265 38 266 40 260 31 253 31 230 60 206 68 198 75 209 66 228 65 243 82 261 84 268 100 267 103 261 77 239 79 231 100 207 98 196 119 201 143 202 160 195 166 210 172 213 173 238 167 251 160 248 154 265 169 264 178 247 186 240 198 260 200 271 217 271 219 262 207 258 195 230 192 198 210 184 227 164 242 144 259 145 284 151 277 141 293 140 299 134 297 127 273 119 270 105
Polygon -7500403 true true -1 195 14 180 36 166 40 153 53 140 82 131 134 133 159 126 188 115 227 108 236 102 238 98 268 86 269 92 281 87 269 103 269 113

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 3D 6.1.1
@#$#@#$#@
need-to-manually-make-preview-for-this-model
@#$#@#$#@
@#$#@#$#@
<experiments>
  <experiment name="timeseries" repetitions="100" runMetricsEveryStep="true">
    <setup>setup</setup>
    <go>go</go>
    <metric>count-malignant-cells</metric>
    <metric>count-somatic-cells</metric>
    <metric>count-vascular-patches</metric>
    <enumeratedValueSet variable="quiescent-duration">
      <value value="15"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="angiogenesis-signal-radius">
      <value value="1"/>
      <value value="2"/>
      <value value="3"/>
      <value value="4"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="initial-cells">
      <value value="500"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="max-low-energy-duration">
      <value value="10"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="malignant-proliferation-chance">
      <value value="2.25"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="angiogenesis-min-demand">
      <value value="1"/>
      <value value="2"/>
      <value value="3"/>
      <value value="4"/>
      <value value="5"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="energy-req-prolif">
      <value value="2"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="normal-proliferation-chance">
      <value value="2"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="disease-ratio">
      <value value="2"/>
    </enumeratedValueSet>
  </experiment>
  <experiment name="final-state" repetitions="50" runMetricsEveryStep="false">
    <setup>setup</setup>
    <go>go</go>
    <metric>count-malignant-cells</metric>
    <metric>count-somatic-cells</metric>
    <metric>count-malignant-spawn-cells</metric>
    <metric>count-somatic-spawn-cells</metric>
    <metric>count-quiescent-cells-at-final-tick</metric>
    <metric>count-quiescent-cells-occurences</metric>
    <metric>count-vascular-patches</metric>
    <enumeratedValueSet variable="quiescent-duration">
      <value value="15"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="angiogenesis-signal-radius">
      <value value="2"/>
      <value value="4"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="initial-cells">
      <value value="250"/>
      <value value="500"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="max-low-energy-duration">
      <value value="10"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="malignant-proliferation-chance">
      <value value="2.25"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="angiogenesis-min-demand">
      <value value="1"/>
      <value value="3"/>
      <value value="5"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="energy-req-prolif">
      <value value="2"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="normal-proliferation-chance">
      <value value="2"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="disease-ratio">
      <value value="2"/>
    </enumeratedValueSet>
  </experiment>
  <experiment name="sensitivity" repetitions="30" runMetricsEveryStep="false">
    <setup>setup</setup>
    <go>go</go>
    <metric>count-malignant-cells</metric>
    <metric>count-somatic-cells</metric>
    <metric>count-malignant-spawn-cells</metric>
    <metric>count-somatic-spawn-cells</metric>
    <metric>count-quiescent-cells-at-final-tick</metric>
    <metric>count-quiescent-cells-occurences</metric>
    <metric>count-vascular-patches</metric>
    <enumeratedValueSet variable="quiescent-duration">
      <value value="15"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="angiogenesis-signal-radius">
      <value value="3"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="initial-cells">
      <value value="500"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="max-low-energy-duration">
      <value value="10"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="malignant-proliferation-chance">
      <value value="2.15"/>
      <value value="2.35"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="angiogenesis-min-demand">
      <value value="3"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="energy-req-prolif">
      <value value="1"/>
      <value value="3"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="normal-proliferation-chance">
      <value value="1.9"/>
      <value value="2.1"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="disease-ratio">
      <value value="2"/>
    </enumeratedValueSet>
  </experiment>
</experiments>
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
